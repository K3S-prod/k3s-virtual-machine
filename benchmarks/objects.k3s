# Check work of GC with objects

.num ONE    1
.num THREE  3
.num FIVE   5

.num VALUE_N 4000000
.num VALUE_M 1000

.str FIELD_constructor "constructor_"
.str FIELD_setY "setY_"
.str FIELD_x "x_"
.str FIELD_y "y_"
.str FIELD_a "a_"

.obj Bar {
    .any a_

    .def constructor_ {
        getthis r0          # r0 = *this
        ldai FIELD_a        # r1 = "a"
        sta r1
        getarg0 r2          # ACC = a
        lda r2
        setelem r0 r1       # r0[r1] = acc
        ret
    }
}

.obj Foo {
    .any x_
    .any y_

    .def constructor_ {
        getthis r0          # r0 = *this
        ldai FIELD_x        # r1 = "x"
        sta r1
        getarg0 r2          # acc = a
        lda r2
        setelem r0 r1       # r0[r1] = acc

        ldai FIELD_y        # r1 = "y"
        sta r1
        stnull r2           # r1 = null
        lda r2
        setelem r0 r1      # r0[r1] = r1
        ret
    }

    .def setY_ {
        getthis r0          # ACC = *this
        ldai FIELD_y        # r1 = "y"
        sta r1
        getarg0 r2          # r0 = b (obj "Bar")
        lda r2
        setelem r0 r1       # r0[r1] = acc
        ret
    }
}

.def foo {
    getarg0 r0      # r0 = N
    getarg1 r1      # r1 = M

    newarr r1       # ACC = alloc(r1(num elements))
    sta r2          # r2(foo) = ACC

    stnull r3
    
    ldai ONE        # ACC(i) = 1
    sta r4          # r4(i) = ACC

loop:
    sub r4 r0       # ACC = r4(i) - r0(N)
    bge loop_end    # jump if (ACC >= 0)

    ldai Foo                # ACC = alloc(Foo(class))
    sta r5                  # r5(o1) = ACC
    ldai FIELD_constructor  # ACC = r5["constructor_"]
    sta r6
    getelem r5 r6

    setarg0 r4              # ACC(Foo_constructor).args[1] = r4(i)
    call                    # ACC()

    ldai THREE      # ACC = 3
    sta r6          # r6 = ACC
    mod r4 r6       # ACC = r4(i) mod r6(3)

    bne skip_1      # jump if (ACC != 0)

    mod r4 r1       # ACC = r4(i) mod r1(M)
    sta r6          # r6 = ACC

    lda r5          # ACC = r5
    setelem r2 r6   # r2[r6(i % M)] = acc(o1)
skip_1:

    ldai Bar                # ACC = alloc(Bar(class))
    sta r6                  # r6(o2) = ACC
    ldai FIELD_constructor  # ACC = Bar:constructor
    sta r7
    getelem r6 r7

    setarg0 r4              # ACC(Bar_constructor).args[0] = r4(i)
    call                    # ACC()

    ldai FIVE       # ACC = 5
    sta r7          # r7 = ACC
    mod r4 r7       # ACC = r4(i) mod r7(5)

    bne skip_2      # jump if (ACC != 0)

    ldai FIELD_setY # ACC = Foo:setY
    sta r7
    getelem r5 r7
    setarg0 r6      # ACC(Foo_setY).args[0] = r6(o2)
    call            # ACC()
skip_2:

    mov r5 r3    # r3(outer) <- r5(o1)
    ldai ONE
    add2 r4
    sta r4
    jump loop
loop_end:

    dump r2
    ret
}

.def main {
    ldai VALUE_N    # ACC = N(4000000)
    sta r0          # r0 = ACC

    ldai VALUE_M    # ACC = M(1000)
    sta r1          # r1 = ACC

    ldai foo        # ACC = foo (func)
    setarg0 r0      # ACC(foo).args[0] = r0(N)
    setarg1 r1      # ACC(foo).args[1] = r1(M)
    call            # ACC()
    ret
}
